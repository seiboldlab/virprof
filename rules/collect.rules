Env(name="virprof_r", base="bioconda",
    packages=[
        "r-magrittr",
        "r-dplyr",
        "r-readr",
        "r-purrr",
        "r-tidyr",
        "r-stringr",
        "r-fuzzyjoin",
        "r-tibble",
        "r-openxlsx",
        "r-optparse",
        "r-tidyverse",
        "r-patchwork",
        "r-ggrepel",
        "bedtools"
    ])

Env(name="virprof_py", base="bioconda",
    packages=[
        "ymp=0.2.1",
        "graph-tool",
    ])


with Stage("blastfilter_vp") as S:
    S.add_param("U", typ="int", name="min_unaligned_bp", default=0)

    rule vp_filter_blast:
        message: "Filtering contigs"
        input:
            fasta  = "{:prev:}/{:target:}.fasta.gz",
            blast7 = "{:prev:}/{:target:}.blast7.gz"
        output:
            fasta  = "{:this:}/{target}.fasta.gz"
        log:
            "{:this:}/{target}.log"
        threads:
            1
        conda:
            "virprof_py"
        shell:
            "python -m virprof filter-blast"
            " --min-unaligned-bp {params.min_unaligned_bp}"
            " --out {output.fasta}"
            " --in-fasta {input.fasta}"
            " --in-blast7 {input.blast7}"
            " >{log} 2>&1"

    rule vp_filter_blast_all:
        message:
            "Finished {output}"
        input:
            "{:this:}/{:targets:}.fasta.gz"
        output:
            touch("{:this:}/all_targets.stamp")


with Stage("blastbin_vp"):
    rule vp_index_tree:
        message: "Creating taxonomy index for virprof bin&classify"
        input:
            ncbi_nodes = "{:prev:}/{target}.NCBI.nodes.dmp",
            ncbi_names = "{:prev:}/{target}.NCBI.names.dmp"
        output:
            "{:this:}/{target}.taxonomy.gt"
        log:
            "{:this:}/{target}.taxonomy.gt.log"
        threads:
            1
        params:
            taxonomy_prefix = lambda wc, input: input.ncbi_names[:-len("names.dmp")]
        conda:
            "virprof_py"
        shell:
            "python -m virprof index-tree"
            "  --library graph_tool"
            "  --ncbi-taxonomy {params.taxonomy_prefix}"
            "  --out {output}"

    rule vp_blastbin:
        message: "Classifying {input.blast7}"
        input:
            coverage = "{:prev:}/{:target:}.coverage",
            fasta    = "{:prev:}/{:target:}.fasta.gz",
            blast7   = "{:prev:}/{:target:}.blast7.gz",
            taxonomy = "{:this:}/ALL.taxonomy.gt"
        output:
            result   = "{:this:}/{target}.virus.csv"
        log:
                       "{:this:}/{target}.log"
        threads:
            1
        conda:
            "virprof_py"
        shell:
            "python -m virprof blastbin"
            " --out {output.result}"
            " --ncbi-taxonomy {input.taxonomy}"
            " --in-blast7 {input.blast7}"
            " --in-coverage {input.coverage}"
            " --in-fasta {input.fasta}"
            " >{log} 2>&1"

    rule vp_aggregate_classifiations:
        message: "Creating virus summary"
        input:
            #virushostdb="{:prev:}/virushostdb.csv",
            calls="{:this:}/{:targets:}.virus.csv"
        output:
            excel="{:this:}/virus_summary.xlsx",
            csv="{:this:}/result_by_sample.csv"
        log:
            "{:this:}/aggregate.log"
        params:
            this="{:this:}",
            filter="^Viruses;",
            filter_host="Homo sapiens",
            min_alen=200,
            min_reads=3
        threads:
            1
        conda:
            "virprof_r"
        shell:
            "./scripts/aggregate_classifications.R"
            "  --filter '{params.filter}'"
            "  --filter-host '{params.filter_host}'"
            "  --min-alen '{params.min_alen}'"
            "  --min-reads '{params.min_reads}'"
            "  --excel-out '{output.excel}'"
            "  --out '{params.this}/result_%s.csv'"
            "  {input.calls}"
            "  >{log} 2>&1"

    rule vp_show_bins:
        message: "Visualizing binning"
        input:
            calls = "{:this:}/{target}.virus.csv",
            bam    = "{:prev:}/{:target:}.sorted.bam"
        output:
            plot =  "{:this:}/{target}.virus.pdf"
        log:
            "{:this:}/{target}.show_bins.log"
        threads:
            1
        conda:
            "virprof_r"
        params:
            page_width = 22,
            page_height = 18,
            plots_per_page = 5,
            min_alen = 200,
            min_reads = 3
        shell:
            "./scripts/show_bins.R"
            " --input {input.calls}"
            " --input-bam {input.bam}"
            " --output {output.plot}"
            " --page-width {params.page_width}"
            " --page-height {params.page_height}"
            " --plots-per-page {params.plots_per_page}"
            " --min-alen {params.min_alen}"
            " --min-reads {params.min_reads}"
            " >{log} 2>&1"

    rule vp_classify_all:
        input:
            summary = "{:this:}/virus_summary.xlsx",
            plots = "{:this:}/{:targets:}.virus.pdf"
        output:
            touch("{:this:}/all_targets.stamp")
